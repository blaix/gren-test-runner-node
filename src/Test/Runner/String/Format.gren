module Test.Runner.String.Format exposing (format)

import Diff exposing (Change(..))
import Test.Runner.Failure exposing (InvalidReason(..), Reason(..))
import Math


format : String -> Reason -> String
format description reason =
    when reason is
        Custom ->
            description

        Equality { expected, actual } ->
            equalityToString { operation = description, expected = expected, actual = actual }

        Comparison { expected, actual } ->
            verticalBar description expected actual

        TODO ->
            description

        Invalid BadDescription ->
            if description == "" then
                "The empty string is not a valid test description."

            else
                "This is an invalid test description: " ++ description

        Invalid _ ->
            description

        ArrayDiff { expected, actual } ->
            listDiffToString 0
                description
                { expected = expected
                , actual = actual
                }
                { originalExpected = expected
                , originalActual = actual
                }

        CollectionDiff { expected, actual, extra, missing } ->
            let
                extraStr =
                    if Array.isEmpty extra then
                        ""

                    else
                        "\nThese keys are extra: "
                            ++ (extra |> String.join ", " |> (\d -> "[ " ++ d ++ " ]"))

                missingStr =
                    if Array.isEmpty missing then
                        ""

                    else
                        "\nThese keys are missing: "
                            ++ (missing |> String.join ", " |> (\d -> "[ " ++ d ++ " ]"))
            in
            String.join ""
                [ verticalBar description expected actual
                , "\n"
                , extraStr
                , missingStr
                ]


verticalBar : String -> String -> String -> String
verticalBar comparison below above =
    [ above
    , "╵"
    , "│ |> " ++ comparison
    , "╷"
    , below
    ]
        |> String.join "\n"


hexInt int =
    if int == 0 then
        "0"

    else
        let
            zeroPad4 n =
                if String.unitLength n < 4 then
                    zeroPad4 ("0" ++ n)

                else
                    n

            hexIntInternal i =
                if i == 0 then
                    ""

                else
                    hexIntInternal (i // 16)
                        ++ (when i |> Math.remainderBy 16 is
                                10 ->
                                    "a"

                                11 ->
                                    "b"

                                12 ->
                                    "c"

                                13 ->
                                    "d"

                                14 ->
                                    "e"

                                15 ->
                                    "f"

                                decimalDigit ->
                                    String.fromInt decimalDigit
                           )
        in
        zeroPad4 (hexIntInternal int)


escapeUnicodeChars s =
    let
        isAsciiChar v =
            (32 <= v && v <= 125)
                -- in ascii a-z A-Z with a few common special characters
                && (-- except ` and ^ because they're combining marks, like in è and ê.
                    Array.member v [ 94, 96 ] == False
                   )
    in
    s
        |> String.toArray
        |> Array.map Char.toCode
        |> Array.map
            (\c ->
                if isAsciiChar c then
                    String.fromChar (Char.fromCode c)

                else
                    "\\u{" ++ hexInt c ++ "}"
            )
        |> String.join ""


listDiffToString :
    Int
    -> String
    -> { expected : Array String, actual : Array String }
    -> { originalExpected : Array String, originalActual : Array String }
    -> String
listDiffToString index description { expected, actual } originals =
    when { l = Array.popFirst expected, r = Array.popFirst actual } is
        { l = Nothing, r = Nothing } ->
            [ "Two lists were unequal previously, yet ended up equal later."
            , "This should never happen!"
            , "Please report this bug to https://github.com/gren-lang/test/issues - and include these lists: "
            , "\n"
            , Debug.toString originals.originalExpected
            , "\n"
            , Debug.toString originals.originalActual
            ]
                |> String.join ""

        { l = Just _, r = Nothing } ->
            verticalBar (description ++ " was shorter than")
                (Debug.toString originals.originalExpected)
                (Debug.toString originals.originalActual)

        { l = Nothing, r = Just _ } ->
            verticalBar (description ++ " was longer than")
                (Debug.toString originals.originalExpected)
                (Debug.toString originals.originalActual)

        { l = Just { first = firstExpected, rest = restExpected }, r = Just { first = firstActual, rest = restActual } } ->
            if firstExpected == firstActual then
                -- They're still the same so far; keep going.
                listDiffToString (index + 1)
                    description
                    { expected = restExpected
                    , actual = restActual
                    }
                    originals

            else
                -- We found elements that differ; fail!
                String.join ""
                    [ verticalBar description
                        (Debug.toString originals.originalExpected)
                        (Debug.toString originals.originalActual)
                    , "\n\nThe first diff is at index "
                    , Debug.toString index
                    , ": it was `"
                    , firstActual
                    , "`, but `"
                    , firstExpected
                    , "` was expected."
                    ]


equalityToString : { operation : String, expected : String, actual : String } -> String
equalityToString { operation, expected, actual } =
    -- TODO make sure this looks reasonable for multiline strings
    let
        { below = { value = valueBelow, arrows = diffArrowsBelow }, above = { arrows = diffArrowsAbove, value = valueAbove } } =
            formatEqualityDiffArrows expected actual

        { below = { value = unicodeValueBelow, arrows = unicodeDiffArrowsBelow }, above = { arrows = unicodeDiffArrowsAbove, value = unicodeValueAbove } } =
            formatEqualityDiffArrows (escapeUnicodeChars expected) (escapeUnicodeChars actual)

        combine things =
            things
                |> Array.map (String.join "")
                |> String.join "\n"
    in
    verticalBar
        operation
        (if String.join "" valueBelow /= String.join "" unicodeValueBelow then
            -- we need to show the escaped string as well
            combine
                [ valueBelow
                , diffArrowsBelow
                , unicodeValueBelow ++ [ " (same string but with unicode characters escaped)" ]
                , unicodeDiffArrowsBelow
                ]

         else
            combine
                [ valueBelow
                , diffArrowsBelow
                ]
        )
        (if String.join "" valueAbove /= String.join "" unicodeValueAbove then
            -- we need to show the escaped string as well
            combine
                [ unicodeDiffArrowsAbove
                , unicodeValueAbove ++ [ " (same string but with unicode characters escaped)" ]
                , diffArrowsAbove
                , valueAbove
                ]

         else
            combine
                [ diffArrowsAbove
                , valueAbove
                ]
        )


formatEqualityDiffArrows : String -> String -> { below : { value : Array String, arrows : Array String }, above : { value : Array String, arrows : Array String } }
formatEqualityDiffArrows below above =
    if String.unitLength below * String.unitLength above > 300 * 300 then
        -- The Diff.diff diffing algorithm is roughly O(len(a) * len(b)), so we need some cutoff point where we just give up on diffing.
        { below = { value = [ below ], arrows = [ " -- skipping diffing because input is too large" ] }
        , above = { arrows = [ " -- skipping diffing because input is too large" ], value = [ above ] }
        }

    else
        let
            { firsts = valueBelow, seconds = diffArrowsBelow } =
                Diff.diff (String.toArray below) (String.toArray above)
                    |> Array.map formatExpectedChange
                    |> collectPairs

            { firsts = diffArrowsAbove, seconds = valueAbove } =
                Diff.diff (String.toArray above) (String.toArray below)
                    |> Array.map formatActualChange
                    |> collectPairs
        in
        { below = { value = valueBelow, arrows = diffArrowsBelow }
        , above = { arrows = diffArrowsAbove, value = valueAbove }
        }

collectPairs : Array { first : a, second : b } -> { firsts : Array a, seconds : Array b }
collectPairs arr =
    Array.foldl 
        (\{ first, second } acc -> 
            { firsts = Array.pushLast first acc.firsts
            , seconds = Array.pushLast second acc.seconds 
            }
        )
        { firsts = [], seconds = [] } 
        arr


formatExpectedChange : Change Char -> { first : String, second : String }
formatExpectedChange diff =
    when diff is
        Added _ ->
            { first = "", second = "" }

        Removed char ->
            { first = String.fromChar char, second = "▲" }

        NoChange char ->
            { first = String.fromChar char, second = " " }


formatActualChange : Change Char -> { first : String, second : String }
formatActualChange diff =
    when diff is
        Added _ ->
            { first = "", second = "" }

        Removed char ->
            { first = "▼", second = String.fromChar char }

        NoChange char ->
            { first = " ", second = String.fromChar char }
